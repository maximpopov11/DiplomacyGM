WS: /[ \t]+/

order: move_order
    | hold_order
    | support_order
    | convoy_order
    | core_order

retreat: retreat_order
       | disband_order

build: build_unit
     | disband_unit

move_order: unit WS MOVE WS province WS

hold_order: unit WS HOLD WS

l_move_order: l_unit WS MOVE WS province

l_hold_order: l_unit WS HOLD

support_order: unit WS ((SUPPORT WS (l_move_order | l_hold_order)) | ((SUPPORT_HOLD | SUPPORT) WS l_unit)) WS

convoy_order: unit WS CONVOY WS l_move_order WS

core_order: unit WS CORE WS

retreat_order: retreat_unit WS RETREAT WS province WS

disband_order: ( retreat_unit WS DISBAND ) | ( DISBAND WS retreat_unit ) WS

disband_unit: ( unit WS DISBAND ) | ( DISBAND WS unit ) WS

build_unit: ( BUILD WS DESCRIPTOR province ) | ( BUILD WS province DESCRIPTOR ) WS

SUPPORT_HOLD.3 : /(support|supports|s)[ \-_]?(hold|holds|h|stand|stands)/i

HOLD.2 : "h"
       | "hold"
       | "holds"
       | "stand"
       | "stands"

MOVE.2 : "-"
       | "–"
       | "−"
       | "->"
       | "–>"
       | "−>"
       | "to"
       | "m"
       | "move"
       | "moves"
       | "moves to"
       | "into"

SUPPORT.2 : "s"
        | "support"
        | "supports"

CONVOY.2 : "c"
        | "convoy"
        | "convoys"

CORE.2 : "core"
       | "cores"

RETREAT.2 : MOVE
          | "r"
          | "retreat"
          | "retreats"

DISBAND.2 : "d"
          | "disband"
          | "disbands"
          | "boom"
          | "explodes"
          | "dies"
          | "drop"
          | "drops"
          | "remove"

BUILD.2 : "b"
        | "build"
        | "place"

unit : (DESCRIPTOR)? province

l_unit : (DESCRIPTOR)? province

retreat_unit : (DESCRIPTOR)? province

DESCRIPTOR.2 : /a|army|cannon|f|fleet|boat|ship/i WS

province : PROVINCE (WS PROVINCE) ~ 0..3

PROVINCE : /(?![fFaA]\s)[a-zA-Z0-9\._'-]+(?=\s)/
