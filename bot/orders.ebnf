WS: /[ \t]+/

order: (move_order
      | hold_order
      | support_order
      | convoy_order
      | core_order) WS
   
retreat: (retreat_order
        | disband_order
	| non_retreat_order) WS

build: ( build_unit
       | disband_unit
	   | waive_order
       | vassal_order
       | liege_order
       | monarchy_order
       | disown_order
       | defect_order
       | non_build_order) WS

move_order: unit WS MOVE WS province

hold_order: unit WS HOLD

l_move_order: l_unit WS MOVE WS province

l_hold_order: l_unit WS HOLD

support_order: unit WS ((SUPPORT WS (l_move_order | l_hold_order)) | ((SUPPORT_HOLD | SUPPORT) WS l_unit))

convoy_order: unit WS CONVOY WS l_move_order

core_order: unit WS CORE

retreat_order: retreat_unit WS RETREAT WS province

disband_order: ( retreat_unit WS DISBAND ) | ( DISBAND WS retreat_unit )

non_retreat_order: (support_order | convoy_order | core_order | build_unit | waive_order | vassal_order | liege_order | monarchy_order | disown_order | defect_order)

disband_unit: ( unit WS DISBAND ) | ( DISBAND WS unit )

build_unit: ( BUILD WS DESCRIPTOR province ) | ( BUILD WS province DESCRIPTOR )

waive_order: ( WAIVE WS NUMBER )

vassal_order: ( VASSALIZE WS province ) | (province WS VASSALIZE)

liege_order: (ALLEGIANCE WS province) | (province WS ALLEGIANCE)

monarchy_order: (MONARCHY WS province) | (province WS MONARCHY)

disown_order: (DISOWN WS province) | (province WS DISOWN)

defect_order: DEFECT

non_build_order: (move_order | hold_order | support_order | convoy_order | core_order)

SUPPORT_HOLD.3 : /(support|supports|s)[ \-_]?(hold|holds|h|stand|stands)/i

DEFECT.2 : "defect"

VASSALIZE.2 : "vassalize"
            | "vassal"

ALLEGIANCE.2 : "liege"
             | "submit"
             | "allegiance"

MONARCHY.2 : "dual monarchy"
           | "d m"

DISOWN.2 : "disown"

HOLD.2 : "h"
       | "hold"
       | "holds"
       | "stand"
       | "stands"

MOVE.2 : "-"
       | "–"
       | "−"
       | "->"
       | "–>"
       | "−>"
       | "to"
       | "m"
       | "move"
       | "moves"
       | "moves to"
       | "into"

SUPPORT.2 : "s"
        | "support"
        | "supports"

CONVOY.2 : "c"
        | "convoy"
        | "convoys"

CORE.2 : "core"
       | "cores"

RETREAT.2 : MOVE
          | "r"
          | "retreat"
          | "retreats"

DISBAND.2 : "d"
          | "disband"
          | "disbands"
          | "boom"
          | "explodes"
          | "dies"
          | "drop"
          | "drops"
          | "remove"

BUILD.2 : "b"
        | "build"
        | "place"

WAIVE.2: "w"
       | "waive"

NUMBER : /[0-9]{1,3}/

unit : (DESCRIPTOR)? province

l_unit : (DESCRIPTOR)? province

retreat_unit : (DESCRIPTOR)? province

DESCRIPTOR.2 : /a|army|cannon|f|fleet|boat|ship/i WS

province : PROVINCE (WS PROVINCE) ~ 0..3

PROVINCE : /(?![fFaA]\s)[a-zA-Z0-9\._‘’`´′‛'-]+(?=\s)/
