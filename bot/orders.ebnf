WS: /[ \t]+/
NL: "\n"

movement_phase: ".order" (WS order?)? (NL (order? WS? NL) *)?

retreat_phase: ".order" (WS retreat?)? (NL (retreat? WS? NL) *)?

order: move_order
    | hold_order
    | support_order
    | convoy_move_order
    | convoy_order
    | core_order

retreat: retreat_order
       | disband_order


move_order: unit WS MOVE WS province

hold_order: unit (WS HOLD)?

support_order: unit WS (SUPPORT WS (move_order | hold_order) | SUPPORT_HOLD WS hold_order)

convoy_move_order: unit WS CONVOY_MOVE WS province

convoy_order: unit WS CONVOY WS move_order

core_order: unit WS CORE

retreat_order: retreat_unit WS RETREAT WS province

disband_order: retreat_unit WS DISBAND

SUPPORT_HOLD.2 : /(support|supports|s)[ \-_]?(hold|holds|h)/

HOLD.2 : "h"
       | "hold"
       | "holds"

MOVE.2 : "-"
       | "->"
       | "to"
       | "m"
       | "move"
       | "moves"

CONVOY_MOVE.2 : "c-"
        | "cm"
        | "convoy -"
        | "convoy ->"
        | "convoy to"
        | "convoy m"
        | "convoy move"
        | "convoy moves"

SUPPORT.2 : "s"
        | "support"
        | "supports"

CONVOY.2 : "c"
        | "convoy"
        | "convoys"

CORE.2 : "core"
    | "cores"

RETREAT.2 : MOVE
          | "r"
          | "retreat"
          | "retreats"

DISBAND.2 : "d"
          | "disband"
          | "disbands"
          | "boom"
          | "explodes"
          | "dies"

unit : (DESCRIPTOR)? province

retreat_unit : (DESCRIPTOR)? province

DESCRIPTOR.2 : /[afAF]/ WS

province : PROVINCE (WS PROVINCE) *

PROVINCE : /(?![fFaA]\s)[a-zA-Z0-9\._'-]+(?=\s)/
